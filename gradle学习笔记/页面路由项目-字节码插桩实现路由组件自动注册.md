## 1、字节码插桩技术说明

利用字节码插桩实现，将多个 RouterMapping_xxx 合并汇总成 RouterMapping

```
public class RouterMapping {
    public static Map<String, String> get() {
        Map<String, String> map = new HashMap<>();

        map.putAll(RouterMapping_1.get());
        map.putAll(RouterMapping_2.get());

        //...

        return map;
    }
}
```

// 通过字节码插桩技术，在.class打包成.dex文件前对字节码进行修改
// 修改字节码，通过ASM技术
1）安装 ASM Bytecode Viewer Support Kotlin 插件，帮助写ASM代码
2）如何查看 com.watayouxiang.androiddemo.sample.RouterMapping 的ASM代码写法：
    在RouterMapping文件中，右键选择 ASM Bytecode Viewer 就能查看RouterMapping的二进制代码，
    再点击 ASMMified 选项卡，就能查看RouterMapping的ASM代码



## 2、编码

### 1）引用Transform插件

在 buildSrc/build.gradle 添加如下

```
// 声明仓库的地址
repositories {
    // 包含 'com.android.tools.build:gradle:xxx'
    google()
}

// 声明依赖的包
dependencies {
    // 包含 com.android.build.api.transform.Transform
    implementation 'com.android.tools.build:gradle:4.1.3'
}
```



### 2）收集所有RouterMapping_xxx

- 在 com.imooc.router.gradle.RouterMappingCollector
  - 收集所有RouterMapping_xxx

```
class RouterMappingCollector {

    private static final String PACKAGE_NAME = 'com/watayouxiang/androiddemo/mapping'
    private static final String CLASS_NAME_PREFIX = 'RouterMapping_'
    private static final String CLASS_FILE_SUFFIX = '.class'

    private final Set<String> mappingClassNames = new HashSet<>()

    /**
     * 获取收集好的映射表类名
     * @return
     */
    Set<String> getMappingClassName() {
        return mappingClassNames
    }

    /**
     * 收集class文件或者class文件目录中的映射表
     * @param classFile
     */
    void collect(File classFile) {
        if (classFile == null || !classFile.exists()) return
        if (classFile.isFile()) {
            if (classFile.absolutePath.contains(PACKAGE_NAME)
                    && classFile.name.startsWith(CLASS_NAME_PREFIX)
                    && classFile.name.endsWith(CLASS_FILE_SUFFIX)) {
                String className = classFile.name.replace(CLASS_FILE_SUFFIX, "")
                mappingClassNames.add(className)
            }
        } else {
            classFile.listFiles().each {
                collect(it)
            }
        }
    }

    /**
     * 收集JAR包中的目标类
     * @param jarFile
     */
    void collectFromJarFile(File jarFile) {
        Enumeration enumeration = new JarFile(jarFile).entries()

        while (enumeration.hasMoreElements()) {
            JarEntry jarEntry = (JarEntry) enumeration.nextElement()
            String entryName = jarEntry.getName()
            if (entryName.contains(PACKAGE_NAME)
                    && entryName.contains(CLASS_NAME_PREFIX)
                    && entryName.contains(CLASS_FILE_SUFFIX)) {
                String className = entryName.replace(PACKAGE_NAME, "")
                        .replace("/", "")
                        .replace(CLASS_FILE_SUFFIX, "")
                mappingClassNames.add(className)
            }
        }
    }
}
```



- 在 com.imooc.router.gradle.RouterMappingTransform
  - 实现类的拷贝逻辑
  - 收集所有RouterMapping_xxx

```
class RouterMappingTransform extends Transform {
    /**
     * 当前 Transform 的名称
     * @return
     */
    @Override
    String getName() {
        return "RouterMappingTransform"
    }

    /**
     * 返回告知编译器，当前 Transform 需要消费的输入类型
     * 在这里是 CLASS 类型
     * @return
     */
    @Override
    Set<QualifiedContent.ContentType> getInputTypes() {
        return TransformManager.CONTENT_CLASS
    }

    /**
     * 告诉编译器，当前 Transform 需要收集的范围
     * @return
     */
    @Override
    Set<? super QualifiedContent.Scope> getScopes() {
        return TransformManager.SCOPE_FULL_PROJECT
    }

    /**
     * 告诉编译器，当前 Transform 是否支持增量
     * 通常返回 false
     * @return
     */
    @Override
    boolean isIncremental() {
        return false
    }

    /**
     * 所有 class 收集好以后，会被打包传入此方法
     * @param transformInvocation
     * @throws TransformException，InterruptedException，IOException
     */
    @Override
    void transform(TransformInvocation transformInvocation) throws TransformException, InterruptedException, IOException {
        //super.transform(transformInvocation)
        // 1、遍历所有的Input
        // 2、对Input进行二次处理
        // 3、将Input拷贝到目标目录

        RouterMappingCollector collector = new RouterMappingCollector()

        // 遍历所有的输入
        // 如果 app module 有：build/intermediates/transforms/RouterMappingTransform，说明拷贝成功
        transformInvocation.inputs.each {
            // 把文件夹类型的输入，拷贝到目标目录
            it.directoryInputs.each { dirInput ->
                def destDir = transformInvocation.outputProvider.getContentLocation(
                        dirInput.name,
                        dirInput.contentTypes,
                        dirInput.scopes,
                        Format.DIRECTORY)
                collector.collect(dirInput.file)
                FileUtils.copyDirectory(dirInput.file, destDir)
            }
            // 把 JAR 类型的输入，拷贝到目标目录
            it.jarInputs.each { jarInput ->
                def dest = transformInvocation.outputProvider.getContentLocation(
                        jarInput.name,
                        jarInput.contentTypes,
                        jarInput.scopes,
                        Format.JAR)
                collector.collectFromJarFile(jarInput.file)
                FileUtils.copyFile(jarInput.file, dest)
            }
        }

        // RouterMappingTransform all mapping class name = [RouterMapping_1617693731656, RouterMapping_1617693732873]
        println("${getName()} all mapping class name = " + collector.mappingClassName)

    }
}
```



### 3）ASM生成RouterMapping

- 在 com.imooc.router.gradle.RouterMappingByteCodeBuilder
  - 用asm编写RouterMapping字节码

```
class RouterMappingByteCodeBuilder implements Opcodes {

    public static final String CLASS_NAME = "com/watayouxiang/androiddemo/mapping/RouterMapping"

    static byte[] get(Set<String> allMappingNames) {
        // 1、创建一个类
        // 2、创建构造方法
        // 3、创建get方法
        //      1）创建一个Map
        //      2）塞入所有映射表的内容
        //      3）返回map

        // ClassWriter.COMPUTE_MAXS 自动计算局部变量需要的栈针大小
        ClassWriter classWriter = new ClassWriter(ClassWriter.COMPUTE_MAXS)
        // 1、创建一个类
        classWriter.visit(V1_8,
                ACC_PUBLIC | ACC_SUPER,
                CLASS_NAME, // 类名
                null,
                "java/lang/Object",// 父类
                null
        )

        MethodVisitor methodVisitor

        // 2、创建构造方法
        methodVisitor = classWriter.visitMethod(ACC_PUBLIC,
                "<init>",
                "()V",
                null,
                null)
        // 开启字节码的生成和访问
        methodVisitor.visitCode()
        methodVisitor.visitVarInsn(ALOAD, 0)
        methodVisitor.visitMethodInsn(INVOKESPECIAL,
                "java/lang/Object",
                "<init>",
                "()V",
                false)
        methodVisitor.visitInsn(RETURN)
        methodVisitor.visitMaxs(1, 1)
        methodVisitor.visitEnd()

        // 3、创建get方法
        methodVisitor = classWriter.visitMethod(ACC_PUBLIC | ACC_STATIC,
                "get",
                "()Ljava/util/Map;",
                "()Ljava/util/Map<Ljava/lang/String;Ljava/lang/String;>;",
                null)
        methodVisitor.visitCode()

        // 1）创建一个Map
        methodVisitor.visitTypeInsn(NEW, "java/util/HashMap")
        methodVisitor.visitInsn(DUP)
        methodVisitor.visitMethodInsn(INVOKESPECIAL,
                "java/util/HashMap",
                "<init>",
                "()V",
                false)
        methodVisitor.visitVarInsn(ASTORE, 0)
        // 2）塞入所有映射表的内容
        allMappingNames.each {
            methodVisitor.visitVarInsn(ALOAD, 0);
            methodVisitor.visitMethodInsn(INVOKESTATIC,
                    "com/watayouxiang/androiddemo/mapping/$it",
                    "get", "()Ljava/util/Map;",
                    false)
            methodVisitor.visitMethodInsn(INVOKEINTERFACE,
                    "java/util/Map",
                    "putAll",
                    "(Ljava/util/Map;)V",
                    true)
        }
        // 3）返回map
        methodVisitor.visitVarInsn(ALOAD, 0)
        methodVisitor.visitInsn(ARETURN)
        methodVisitor.visitMaxs(2, 1)

        classWriter.visitEnd()

        return classWriter.toByteArray()
    }

}
```



- 在com.imooc.router.gradle.RouterMappingTransform
  - 将生成的RouterMapping字节码插入到本地文件

```
class RouterMappingTransform extends Transform {

		//......
		
    /**
     * 所有 class 收集好以后，会被打包传入此方法
     * @param transformInvocation
     * @throws TransformException，InterruptedException，IOException
     */
    @Override
    void transform(TransformInvocation transformInvocation) throws TransformException, InterruptedException, IOException {
        //super.transform(transformInvocation)
        // 1、遍历所有的Input
        // 2、对Input进行二次处理
        // 3、将Input拷贝到目标目录

        RouterMappingCollector collector = new RouterMappingCollector()

        // 遍历所有的输入
        // 如果 app module 有：build/intermediates/transforms/RouterMappingTransform，说明拷贝成功
        transformInvocation.inputs.each {
            // 把文件夹类型的输入，拷贝到目标目录
            it.directoryInputs.each { dirInput ->
                def destDir = transformInvocation.outputProvider.getContentLocation(
                        dirInput.name,
                        dirInput.contentTypes,
                        dirInput.scopes,
                        Format.DIRECTORY)
                collector.collect(dirInput.file)
                FileUtils.copyDirectory(dirInput.file, destDir)
            }
            // 把 JAR 类型的输入，拷贝到目标目录
            it.jarInputs.each { jarInput ->
                def dest = transformInvocation.outputProvider.getContentLocation(
                        jarInput.name,
                        jarInput.contentTypes,
                        jarInput.scopes,
                        Format.JAR)
                collector.collectFromJarFile(jarInput.file)
                FileUtils.copyFile(jarInput.file, dest)
            }
        }

        // RouterMappingTransform all mapping class name = [RouterMapping_1617693731656, RouterMapping_1617693732873]
        println("${getName()} all mapping class name = " + collector.mappingClassName)

        //-------------------------------- 使用RouterMappingByteCodeBuilder
        File mappingJarFile = transformInvocation.outputProvider.getContentLocation(
                "router_mapping",
                getOutputTypes(),
                getScopes(),
                Format.JAR)

        println("${getName()} mappingJarFile = $mappingJarFile")

        if (mappingJarFile.getParentFile().exists()) {
            mappingJarFile.getParentFile().mkdirs()
        }
        if (mappingJarFile.exists()) {
            mappingJarFile.delete()
        }

        // 将生成的字节码写入本地文件
        FileOutputStream fos = new FileOutputStream(mappingJarFile)
        JarOutputStream jarOutputStream = new JarOutputStream(fos)
        ZipEntry zipEntry = new ZipEntry(RouterMappingByteCodeBuilder.CLASS_NAME + ".class")
        jarOutputStream.putNextEntry(zipEntry)
        jarOutputStream.write(RouterMappingByteCodeBuilder.get(collector.mappingClassName))

        jarOutputStream.closeEntry()
        jarOutputStream.close()
        fos.close()
    }
}
```



### 4）验证生成结果

```
$ ./gradlew clean
$ ./gradlew :androiddemo:assembleDebug -q

日志输出：RouterMappingTransform mappingJarFile = /Users/TaoWang/Documents/Code/github/Android/androiddemo/build/intermediates/transforms/RouterMappingTransform/debug/48.jar
查看build目录：androiddemo/build/intermediates/transforms/RouterMappingTransform/debug/48.jar

$ cd /Users/TaoWang/Documents/Code/github/Android/androiddemo/build/intermediates/transforms/RouterMappingTransform/debug
// 将 48.jar 解压到 48
$ unzip 48.jar -d 48

查看解压后的 RouterMapping.class 代码，验证正确性
```